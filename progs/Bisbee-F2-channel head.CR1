'CR1000 Series Datalogger
' Modify program from Quinn Hall and send variable to CR300 logger as photo trigger
' By Mike Yao
'Triggers 12V current
'Author: Quinn Hull
' PakBus ID 62 for this CR1000 and will send to Trigger logger CR300 ID 61, same table and variables 
' used in the CR300 logger

'Change Log
'10022022: Create Trigger for 12V based on counter
'10162022: Create Trigger for 12V based on reconnyx
'12142022: Back to measuring inundation as a raw voltage
'12152022: Moved to CR300 from CR1000
'02132023: For three sensors
'04152023: Changes for Mike
'05012023: Updates for field implementation, QH
' Wiring Diagram
' 

'Declare Variables and Units
Public BattV
Public PTemp_C
Public Inundation(3), V(4), PT(4)
Public TrigFlag As Boolean ' if inundated, True. To decide if trigger (SW12) is needed
Public TrigVal As Long ' if inundated, oscillates True False with Scan. To send a trigger (SW12). Note that camera responds to the 'change', not state, of SW12. 
Public Threshold As Float ' inundation in excess sets TrigFlag to True
Public ThresholdPT As Float
Public slope As Float
Public intercept As Float

Units BattV=Volts
Units PTemp_C=Deg C
Units Threshold=mV/mv
Units Inundation()=mV/mV
' Units V()=?
' Units PT()=?

'Define Data Tables
DataTable(InundationData,True,-1)
	DataInterval(0,60,Sec,10)
  Average(1,BattV,FP2,False)
	Average(1,PTemp_C,FP2,False)
	Average(1,Threshold,FP2,False)
	Average(1,ThresholdPT,FP2,False)
	Average(1,TrigFlag,FP2,False)
	Average(1,TrigVal,FP2,False)
	Sample (4,V(1),FP2)
	Sample (4,PT(1),FP2)
EndTable


Sub IfTransducer(PTarr(4), PTthresh, PTbool)
' sub IfTransducer, sets the trigger value to true of median is above threshold
' PTarr(4) : local variable for four PTs (including baro)
' PTthresh : threshold at which the boolean is set to true
' bool : exactly equivalent to the inundation flag, TrigFlag
  Public MaxPT(2), MinPT(2)
  MaxSpa(MaxPT(),3,PTarr())
  MinSpa(MinPT(),3,PTarr())
  If PT(6 - MaxPT(2) - MinPT(2)) > PTthresh Then ' cheeky way to declare spatial median for three 
    PTbool = true
  EndIf 
EndSub

Sub IfInundation(Inarr(4),Inthresh, Inbool)
  ' sub ifInundated(Inundation, threshold)
  Public MinIn(2)
  MinSpa(MinIn(),3,Inarr())
  If MinIn(1) < Inthresh Then 
    Inbool = true
  EndIf 
EndSub

'Main Program
BeginProg
  TrigFlag = false 
  TrigVal = 0
  Threshold = 0.8
  ThresholdPT = 3
' Apply transducer calibration
  slope=0.4554
  intercept=80 ' for baro only
  
	'Main Scan
	Scan(10,Sec,1,0)
	  TrigFlag = false 
	 
		Battery(BattV)
		PanelTemp(PTemp_C,60)
		
		'Generic Half Bridge measurements 'Inundation'
		BrHalf(Inundation(1),1,mV2500,1,VX1,1,500,True,0,60,1,0)
		BrHalf(Inundation(2),1,mV2500,2,VX1,1,500,True,0,60,1,0)
		BrHalf(Inundation(3),1,mV2500,3,VX1,1,500,True,0,60,1,0)
    '	Take 4 transducer readings, V1-V3 for piezometers and V4 is for reference	
    BrHalf (V(1),1,mV2500,5,Vx2,1,2500,False ,0,250,2500,0)
		BrHalf (V(2),1,mV2500,6,Vx2,1,2500,False ,0,250,2500,0)
		BrHalf (V(3),1,mV2500,7,Vx2,1,2500,False ,0,250,2500,0)
    BrHalf (V(4),1,mV2500,8,Vx2,1,2500,False ,0,250,2500,0)    
    ' Baro-correct transducer readings
    PT(1)=(V(1)-V(4))*slope 
    PT(2)=(V(2)-V(4))*slope
    PT(3)=(V(3)-V(4))*slope
    PT(4)=V(4)*slope + intercept
		 
    Call IfTransducer(PT(), ThresholdPT, TrigFlag)
    Call IfInundation(Inundation(), Threshold, TrigFlag)

    ' sub changeTrigger(TrigFlag, TrigVal)
    If (TrigFlag = true) Then
      If TrigVal = 0 Then
        TrigVal = 1
        SendVariables (Inundation(),ComSDC7,0,61,0000,0,"InundationData","Inundation()",Inundation(),3)
      Else
        TrigVal = 0
      EndIf
    ElseIf (TrigFlag = false) Then 
      TrigVal = TrigVal 
    EndIf

    'Delay (0,250,mSec)
    'SW12(TrigVal)
    
		'Call Data Tables and Store Data
		CallTable InundationData

	NextScan
EndProg

